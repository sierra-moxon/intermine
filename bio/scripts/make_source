#!/usr/bin/perl

use strict;
use warnings;
use File::Find;
use File::Path;
use Cwd;

my $BIO_DIR = 'bio';
my $STRUCTURE_DIR = "$BIO_DIR/source-structure";

sub usage
{
  my $message = shift;

  die "$0: $message\n";
}

if (@ARGV != 2) {
  usage (<<USAGE);
needs two arguments:
  - the source name (eg. foobase-gene-data)
  - the source type (eg. custom-file)
USAGE
}

my $source_name = shift;
my $source_type = shift;
my $source_dir = lc $source_name;

my %project_properties = (
                          'custom-file' => <<PROPS
have.file.custom.tgt = true
converter.class = org.intermine.bio.dataconversion.WormBaseIdentifiersConverter
PROPS
                          );

my %substitutions = (
                     'source-name' => $source_name,
                     'source-dir' => $source_dir,
                    );

if (exists $project_properties{$source_type}) {
  $substitutions{'project-properties'} = $project_properties{$source_type}
} else {
  usage ("unknown source type: $source_type");
}

# check that we're in the right part of checkout
if (!-d 'imbuild' || !-d 'intermine') {
  usage (<<MESSAGE);
need to run in the top level of an intermine checkout (containing
"imbuild" and "intermine")
MESSAGE
}

my $start_dir = getcwd;

sub process
{
  my $orig_path = "$start_dir/$File::Find::name";
  my $dest_path = $File::Find::name;

  $dest_path =~ s:$STRUCTURE_DIR(.*):$start_dir/$BIO_DIR/$source_dir$1:;

  if (-d $orig_path) {
    mkpath($dest_path);
  } else {
    open my $orig, '<', $orig_path or die "can't open $orig_path: $!\n";
    open my $dest, '>', $dest_path or die "can't open $dest_path: $!\n";

    while (my $line = <$orig>) {
      for my $subs_key (keys %substitutions) {
        $line =~ s/\$\{$subs_key\}/$substitutions{$subs_key}/;
      }

      print $dest $line;
    }

    close $orig or die "can't close $orig_path: $!\n";
    close $dest or die "can't close $dest_path: $!\n";
  }
}

sub preprocess
{
  return grep {$_ !~ /^(\.svn|.*~)$/} @_;
}

find({
      wanted => \&process, 
      preprocess => \&preprocess
     },
     $STRUCTURE_DIR);
